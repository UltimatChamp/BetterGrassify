plugins {
    id "dev.architectury.loom" version "1.9-SNAPSHOT"
    id "me.modmuss50.mod-publish-plugin" version "0.8.3"
}

def loader = property("loom.platform")

def isFabric = loader == "fabric"
def isNeo = loader == "neoforge"
def isForge = loader == "forge"

def isSnapshot = false
def mcVer = property("deps.minecraft_version")
if (mcVer.contains("-") || mcVer.contains("w")) {
    isSnapshot = true
    mcVer.replace("-", "")
}

version = project.mod_version + "+" + loader + "." + mcVer
group = project.maven_group

base {
    archivesName = project.mod_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.su5ed.dev/releases" }
    maven { url "https://maven.neoforged.net/releases" }
    //maven { url "https://maven.quiltmc.org/repository/release" }
    mavenCentral()
}

loom {
    runConfigs.all {
        ideConfigGenerated true
        runDir "../../run"
    }

    if (loader == "forge") {
        forge {
            mixinConfig "bettergrass.mixins.json"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${property("deps.minecraft_version")}"
    mappings(loom.layered {
        mappings("net.fabricmc:yarn:${property("deps.yarn_mappings")}:v2")
        if (stonecutter.eval(property("deps.minecraft_version"), ">=1.21")) {
            mappings("dev.architectury:yarn-mappings-patch-neoforge:${property("deps.layered_mappings")}")
        }
    })

    if (isFabric) {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${property("deps.fapi_version")}"
        modImplementation "maven.modrinth:modmenu:${property("deps.modmenu_version")}"
        modImplementation "maven.modrinth:sodium:${property("deps.sodium_version")}"
    } else if (isNeo) {
        neoForge "net.neoforged:neoforge:${property("deps.neoforge")}"

        modImplementation "org.sinytra.forgified-fabric-api:forgified-fabric-api:${property("deps.fapi_version")}"
        modImplementation "maven.modrinth:sodium:${property("deps.sodium_version")}"
    } else if (isForge) {
        forge "net.minecraftforge:forge:${property("deps.fml")}"

        modImplementation "dev.su5ed.sinytra.fabric-api:fabric-api:${property("deps.fapi_version")}"
        compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
        modImplementation(include("io.github.llamalad7:mixinextras-forge:0.4.1"))
        modImplementation "maven.modrinth:embeddium:${property("deps.embeddium_version")}"
    }

    include(modImplementation("blue.endless:jankson:${property("deps.jankson_version")}"))
    modImplementation "maven.modrinth:yacl:${property("deps.yacl_version")}"
    //modImplementation "org.quiltmc.parsers:gson:0.3.1"
}

if (stonecutter.current.isActive) {
    rootProject.tasks.register("buildActive") {
        group = "project"
        dependsOn(tasks.named("build"))
        dependsOn(tasks.named("publishMods"))
    }
}

processResources {
    var replaceProperties = [
            minecraft_range      : project.property("deps.mc_range"),
            mod_id               : mod_id,
            mod_name             : mod_name,
            mod_license          : mod_license,
            mod_version          : project.version,
            mod_authors          : mod_authors,
            mod_description      : mod_description,
            sodium_incompat_range: project.property("deps.sodium_incompat_range"),
            mixins               : project.property("deps.mixins")
    ]
    replaceProperties.each { key, value -> inputs.property(key, value) }

    filesMatching("bettergrass.mixins.json") {
        expand replaceProperties
    }

    if (isFabric) {
        filesMatching("fabric.mod.json") {
            expand replaceProperties
        }
        exclude(["META-INF/mods.toml", "META-INF/neoforge.mods.toml"])
    } else if (isNeo) {
        filesMatching("META-INF/neoforge.mods.toml") {
            expand replaceProperties
        }
        exclude(["fabric.mod.json", "META-INF/mods.toml"])
    } else if (isForge) {
        filesMatching("META-INF/mods.toml") {
            expand replaceProperties
        }
        exclude(["fabric.mod.json", "META-INF/neoforge.mods.toml"])
    }

    def packFormat
    switch (project.property("deps.minecraft_version")) {
        case "1.20.1":
            packFormat = 15
            break
        case "1.20.6":
            packFormat = 32
            break
        case "1.21.1":
            packFormat = 34
            break;
        case "1.21.3":
            packFormat = 42
            break
        default:
            packFormat = 46
    }

    filesMatching("pack.mcmeta") {
        expand("pack_format": packFormat)
    }
}

java {
    withSourcesJar()

    def java = stonecutter.eval(project.property("deps.minecraft_version"), ">=1.20.5")
            ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

    sourceCompatibility = java
    targetCompatibility = java
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

publishMods {
    if (mod_version.contains("alpha")) {
        type = ALPHA
    } else if (mod_version.contains("beta") || mod_version.contains("rc") || isSnapshot) {
        type = BETA
    } else {
        type = STABLE
    }

    changelog = "# ${project.version}\n" + file("../../CHANGELOG.md").text
    file = remapJar.archiveFile
    additionalFiles.from(remapJar.archiveFile)
    displayName = "BetterGrassify ${project.version}"

    def fapi
    if (isFabric) {
        fapi = "fabric-api"
        modLoaders.addAll("fabric", "quilt")
    } else if (isNeo) {
        fapi = "forgified-fabric-api"
        modLoaders.add("neoforge")
    } else {
        fapi = "forgified-fabric-api"
        modLoaders.add("forge")
    }

    def mrOptions = modrinthOptions {
        projectId = "m5T5xmUy"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")

        requires(fapi)
        optional("yacl")

        // Discord
        announcementTitle = "Download from Modrinth"
    }

    def cfOptions = curseforgeOptions {
        projectId = "1026394"
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")

        requires(fapi)
        optional("yacl")

        // Discord
        announcementTitle = "Download from CurseForge"
        projectSlug = "bettergrassify"
    }

    if (project.property("deps.minecraft_version") == "1.20.1") {
        modrinth("m1.20.1") {
            from mrOptions

            if (isFabric) {
                minecraftVersionRange {
                    start = "1.20"
                    end = "1.20.4"
                }
            } else {
                minecraftVersions.add("1.20.1")
            }
        }

        curseforge("c1.20.1") {
            from cfOptions

            if (isFabric) {
                minecraftVersionRange {
                    start = "1.20"
                    end = "1.20.4"
                }
            } else {
                minecraftVersions.add("1.20.1")
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.20.6") {
        modrinth("m1.20.6") {
            from mrOptions

            minecraftVersionRange {
                start = "1.20.5"
                end = "1.20.6"
            }
        }

        curseforge("c1.20.6") {
            from cfOptions

            minecraftVersionRange {
                start = "1.20.5"
                end = "1.20.6"
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.21.1") {
        modrinth("m1.21.1") {
            from mrOptions

            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }
        }

        curseforge("c1.21.1") {
            from cfOptions

            minecraftVersionRange {
                start = "1.21"
                end = "1.21.1"
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.21.3") {
        modrinth("m1.21.3") {
            from mrOptions

            minecraftVersionRange {
                start = "1.21.2"
                end = "1.21.3"
            }
        }

        curseforge("c1.21.3") {
            from cfOptions

            minecraftVersionRange {
                start = "1.21.2"
                end = "1.21.3"
            }
        }
    }

    if (project.property("deps.minecraft_version") == "1.21.4") {
        modrinth("m1.21.4") {
            from mrOptions

            minecraftVersions.add("1.21.4")
        }

        curseforge("c1.21.4") {
            from cfOptions

            minecraftVersions.add("1.21.4")
        }
    }

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "UltimatChamp/BetterGrassify"
        commitish = "main"

        // Discord
        announcementTitle = "Download from GitHub"
    }

    discord {
        webhookUrl = providers.environmentVariable("DISCORD_WEBHOOK")
        username = "BetterGrassify Releases"
        avatarUrl = "https://cdn.modrinth.com/data/m5T5xmUy/c67c1f900e8344e462bb5c21fb512579f3b0be46.png"
        content = changelog.map { "@Ping\n" + it }

        style {
            look = "MODERN"
            link = "BUTTON"
            thumbnailUrl = "https://cdn.modrinth.com/data/m5T5xmUy/images/3eaeda7d3cc1cec804e5176d9a21d8eb0f7ab8b6.png"
        }
    }
}
